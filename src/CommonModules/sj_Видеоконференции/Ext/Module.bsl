
#Область ПрограммныйИнтерфейс

// Создает конференцию в сервисе и возвращает ссылку на нее.
//
// Параметры:
//  Название - Строка - Название конференции.
// 
// Возвращаемое значение:
//  Строка - URL - сылка на конференцию.
//
Функция СоздатьКонференцию(Название = "") Экспорт
	
	ТелоЗапроса = НовоеТелоЗапроса();
	
	Если ЗначениеЗаполнено(Название) Тогда
		ТелоЗапроса.roomTitle = Название;
	КонецЕсли;

	АдресРесурса = АдресРесурса();
	
	Ответ = PostJson(АдресРесурса, ТелоЗапроса);

	СсылкаНаКонференцию = Ответ.Получить("url");
	
	Возврат СсылкаНаКонференцию;
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция НовоеТелоЗапроса()

	ТелоЗапроса = Новый Структура;
	ТелоЗапроса.Вставить("roomType", ТипПоУмолчанию());
	ТелоЗапроса.Вставить("roomTitle", "Видеовстреча");
	
	Возврат ТелоЗапроса;

КонецФункции

Функция АдресРесурса()

	Возврат "bk.salutejazz.ru/room/create";

КонецФункции

Функция ТипПоУмолчанию()

	Тип = "ANONYMOUS";
	
	Возврат Тип;

КонецФункции

// Возвращается имя события журнала регистрации для записи ошибок.
//
// Возвращаемое значение:
//	Строка - Имя события ошибки.
//
Функция СобытиеЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Интеграция с сервисом SaluteJazz'");
	
КонецФункции

Функция PostJson(URL, Json)

	Коннектор = ОбщегоНазначения.ОбщийМодуль("КоннекторHTTP");

	Возврат Коннектор.PostJson(URL, Json);
	
КонецФункции

#КонецОбласти